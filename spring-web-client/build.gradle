plugins{
    id "java"
    id "org.openapi.generator" version "$openApiGraglePluginVersion"
    id 'io.spring.dependency-management' version "$springDependencyManagement"
}

publishing {
    repositories {
        maven {
            url = "${artifactory_contextUrl}/tech-task-gradle-release-local"
            credentials{
                username "${artifactory_user}"
                password "${artifactory_password}"
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            groupId = 'tech.task.d3.cloud.api'
            artifactId = "tech-task-"+project.name
            version = "${version}"
            from components.java
        }
    }
}

openApiValidate {
    inputSpec = "${rootDir}/yaml-spec/openapi.yaml".toString()
}

openApiGenerate {
    generatorName = "spring"
    library = "spring-cloud"
    inputSpec = "$rootDir/yaml-spec/openapi.yaml".toString()
    //TODO:remove templateDir and its content when the next version of openapi plugin is released
    templateDir = "$projectDir/spring-cloud-template/"
    outputDir = "$projectDir".toString()
    apiPackage = "tech.task.api"
    modelPackage = "tech.task.api.model"
    invokerPackage = "tech.task.api.invoker"

    configOptions = [
            configPackage: "tech.task.api.config",
            interfaceOnly          : "false",
            skipDefaultInterface   : "true",
            hideGenerationTimestamp: "true",
            generateSupportingFiles: "false",
            useFeignClientUrl      : "true",
            useTags                : "true",
            async                  : "true"
    ]
}

task customCleanUp(type:Delete) {
    delete "$rootDir/spring-web-client/src"
}

tasks.clean.dependsOn(tasks.customCleanUp)

dependencies {
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign:3.1.3'
    implementation 'org.springframework.cloud:spring-cloud-starter-oauth2:2.2.5.RELEASE'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client:2.7.1'
    implementation 'org.springframework.boot:spring-boot-starter-validation:2.7.0'

    implementation 'org.openapitools:jackson-databind-nullable:0.2.2'
    // The Feign client does not support PATCH requests, this dependency is necessary to enable them
    implementation 'io.github.openfeign:feign-okhttp:11.7'

    implementation 'io.springfox:springfox-swagger2:3.0.0'
    implementation 'io.springfox:springfox-swagger-ui:3.0.0'
    implementation 'org.testng:testng:7.1.0'
    implementation 'org.testng:testng:7.1.0'

    testImplementation 'org.mockito:mockito-core:4.6.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

compileJava {
    dependsOn(tasks.openApiGenerate)
}

