import org.openapitools.generator.gradle.plugin.tasks.GenerateTask
import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'java'
    id "org.springframework.boot" version "${springBootVersion}"
    id 'io.spring.dependency-management' version "$springDependencyManagement"
    id "com.github.johnrengelman.processes" version "0.5.0"
    id "org.springdoc.openapi-gradle-plugin" version "1.3.3"
    id "org.openapi.generator" version "$openApiGraglePluginVersion" apply false
}

allprojects {
    repositories {
        maven {
            url = "${artifactory_contextUrl}/tech-task-gradle-dev-local/"
            credentials{
                username "${artifactory_user}"
                password "${artifactory_password}"
            }
        }
    }

    group 'tech.task.d3.cloud.api'

    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'

    bootJar {
        enabled = false
    }

    jar {
        enabled = true
    }
}

subprojects{
    apply plugin: "org.openapi.generator"
    apply plugin: 'maven-publish'

    tasks.withType(GenerateTask) {
        outputDir = "$projectDir".toString()
        generatorName = "spring"
        configOptions = [
                interfaceOnly          : "true",
                skipDefaultInterface   : "true",
                hideGenerationTimestamp: "true",
                generateSupportingFiles: "true",
                useTags                : "true"
        ]
    }

    publishing {
        repositories {
            maven {
                url = "${artifactory_contextUrl}/tech-task-gradle-release-local"
                credentials{
                    username "${artifactory_user}"
                    password "${artifactory_password}"
                }
            }
        }
        publications {
            mavenJava(MavenPublication) {
                groupId = 'tech.task.d3.cloud.api'
                artifactId = "tech-task-"+project.name
                version = "${version}"
                from components.java
            }
        }
    }

    dependencyManagement {
        imports {
            mavenBom(SpringBootPlugin.BOM_COORDINATES)
        }
    }

    test {
        useJUnitPlatform()
    }
}

task openApiGenerate {
    subprojects.each { dependsOn("${it.name}:openApiGenerate") }
}
